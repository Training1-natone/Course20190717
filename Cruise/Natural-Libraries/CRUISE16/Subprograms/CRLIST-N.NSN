* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram CRLIST-N.
/**
/** :author sagadmin
/* 
/* Natural Advanced exercise
DEFINE DATA 
PARAMETER USING NCCOMM-P /* Comm Parameters
PARAMETER USING NCCRUL-P /* Cruise Paramaters
*
*
LOCAL USING NCDATA-L
*
LOCAL
1 C-RECCNT (P10)

1 V-SERVICENAME (A20) CONST<'CR-LIST'>
1 V-WORKREC (A80)
1 V-WORKFIELD (A16) 
1 LOCAL-AVAIL (N1)
*
END-DEFINE
*
* ------------------------------------------------------------------
ON ERROR
  REDUCE ARRAY P-CRUISE-DATA TO  0
  INCLUDE ERRLOG-I 'V-SERVICENAME'
  ESCAPE ROUTINE
END-ERROR
* ------------------------------------------------------------------
MOVE ALL '-' TO V-WORKFIELD
INCLUDE STALOG-I 'V-SERVICENAME'
MOVE P-LANG TO MSG-GROUP-PARA.MSG-LANG
* ------------------------------------------------------------------
*


*
MOVE 0 TO P-RESPONSE.P-RSPCODE
RESET P-NUMBER-OF-RECORDS
*

READ NCCRUISE BY START-DATE

IF NCCRUISE.CRUISE-STATUS IS (N1) 
        COMPUTE LOCAL-AVAIL = VAL(NCCRUISE.CRUISE-STATUS)
        ELSE
          LOCAL-AVAIL := 0
        END-IF

   
  IF LOCAL-AVAIL EQ 0      /* cruise is fully booked
   ESCAPE TOP
  END-IF
        
  IF P-STARTHARBOR NE ' ' AND NCCRUISE.START-HARBOR NE P-STARTHARBOR
    ESCAPE TOP
  END-IF
  IF P-DESTHARBOR NE ' ' AND NCCRUISE.DESTINATION-HARBOR NE P-DESTHARBOR
    ESCAPE TOP
  END-IF

ADD 1 TO C-RECCNT

EXPAND ARRAY P-CRUISE-DATA TO (1:C-RECCNT)
*

MOVE NCCRUISE.CRUISE-ID TO P-CRUISE-DATA.CRUISE-ID(C-RECCNT)
MOVE NCCRUISE.START-HARBOR TO P-CRUISE-DATA.START-HARBOR(C-RECCNT)
MOVE NCCRUISE.DESTINATION-HARBOR TO P-CRUISE-DATA.DESTINATION-HARBOR(C-RECCNT)
MOVE EDITED  NCCRUISE.START-DATE (EM=9999'-'99'-'99) TO P-CRUISE-DATA.START-DATE(C-RECCNT)


* Natural Advanced - exercise -  activate reading a LOB to see performance differences
* F1.
* FIND YACHT-PICTURE WITH YACHT-PICTURE.YACHT-ID = NCCRUISE.ID-YACHT
* COMPRESS YACHT-PICTURE.YACHT-NAME INTO P-CRUISE-DATA.YACHT-NAME(C-RECCNT)
*     MOVE YACHT-PICTURE.PICTURE  TO P-CRUISE-DATA.PICTURE(C-RECCNT)
*     MOVE YACHT-PICTURE.L@PICTURE  TO P-CRUISE-DATA.PICTURELEN(C-RECCNT)
*     ESCAPE BOTTOM (F1.)
*   END-FIND

IF C-RECCNT  = P-OFFERCOUNT THEN
 ESCAPE BOTTOM
END-IF

END-READ


DECIDE FOR FIRST CONDITION
  WHEN C-RECCNT = 0
    MOVE 9857 TO MSG-GROUP-PARA.MSG-NR
  WHEN NONE
    MOVE 9807 TO MSG-GROUP-PARA.MSG-NR
    MOVE C-RECCNT TO P-NUMBER-OF-RECORDS

END-DECIDE


* ------------------------------------------------------------------
CALLNAT 'CAMSG-N' MSG-GROUP-PARA
COMPRESS MSG-GROUP-PARA.MSG-TYPE '-' MSG-GROUP-PARA.MSG-TEXT 
  INTO P-RESPONSE.P-RSPTXT
MOVE MSG-GROUP-PARA.MSG-NR TO P-RESPONSE.P-RSPCODE  
* ------------------------------------------------------------------
INCLUDE ENLLOG-I 'V-SERVICENAME'
* ------------------------------------------------------------------
*
*
END
