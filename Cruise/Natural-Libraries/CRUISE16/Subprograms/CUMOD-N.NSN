* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram CUMOD-N.
/**
/** :author Training
/* 
/* Natural Advanced exercise
DEFINE DATA 
PARAMETER USING NCCOMM-P
PARAMETER USING NCCUGE-P 
*
LOCAL USING NCDATA-L
*
LOCAL
1 V-PERSONID (N8)
1 V-WORKREC (A80)
1 V-WORKFIELD (A16) 
1 V-SERVICENAME (A20) CONST<'CU-MOD'>
*
END-DEFINE
*
* ------------------------------------------------------------------
ON ERROR
  INCLUDE ERRLOG-I 'V-SERVICENAME' 
  ESCAPE ROUTINE
END-ERROR
* ------------------------------------------------------------------
MOVE ALL '-' TO V-WORKFIELD
INCLUDE STALOG-I 'V-SERVICENAME' 
MOVE P-LANG TO MSG-GROUP-PARA.MSG-LANG
* ------------------------------------------------------------------

IF #STUDENT  THEN

  MOVE 9999 TO MSG-GROUP-PARA.MSG-NR

ELSE

COMPUTE V-PERSONID = VAL(P-SELECT.P-PERSON-ID)

F1.
FIND (1) NCCUSTOMER WITH PERSON-ID =  V-PERSONID
    IF NO RECORDS FOUND
      MOVE 9924 TO MSG-GROUP-PARA.MSG-NR
      ESCAPE BOTTOM (F1.)
    END-NOREC
 
  IF NCCUSTOMER.TIMESTAMP =  P-SELECT.P-TIMESTAMP
 THEN
* the record was not updated between GET and this update, update the record
 COMPRESS P-CUSTOMER-DATA.FIRST-NAME-1 INTO NCCUSTOMER.FIRST-NAME-1
 COMPRESS P-CUSTOMER-DATA.SURNAME INTO NCCUSTOMER.SURNAME
 MOVE EDITED NCCUSTOMER.BIRTH-DATE (EM=9999'-'99'-'99) TO P-CUSTOMER-DATA.BIRTH-DATE
 COMPRESS P-CUSTOMER-DATA.EMAIL INTO NCCUSTOMER.EMAIL(1)
 COMPRESS P-CUSTOMER-DATA.STREET-NUMBER INTO NCCUSTOMER.STREET-NUMBER
 COMPRESS P-CUSTOMER-DATA.ZIP-CODE INTO NCCUSTOMER.ZIP-CODE
 COMPRESS P-CUSTOMER-DATA.CITY INTO NCCUSTOMER.CITY
 MOVE *TIMESTMP TO NCCUSTOMER.TIMESTAMP P-CUSTOMER-DATA.TIMESTAMP
  UPDATE (F1.) 
 END TRANSACTION
 ELSE
* the record got updated, so move back an error and do no update 
 MOVE 9934 TO MSG-GROUP-PARA.MSG-NR
 END-IF
END-FIND 
END-IF
* ------------------------------------------------------------------
CALLNAT 'CAMSG-N' MSG-GROUP-PARA
COMPRESS MSG-GROUP-PARA.MSG-TYPE '-' MSG-GROUP-PARA.MSG-TEXT 
  INTO P-RESPONSE.P-RSPTXT
MOVE MSG-GROUP-PARA.MSG-NR TO P-RESPONSE.P-RSPCODE  
* ------------------------------------------------------------------
* ------------------------------------------------------------------
*
INCLUDE GCULOG-I 'V-SERVICENAME' 
*
*
END

